Function Chart_trend(sChartSheet As String, iChartType As Variant, sChartSetting As String)
' this function request data formated as Chart_new
Dim wsChartSheet
Dim x1Row, x1Col ' series X start cell
Dim x2Row, x2Col ' series X end cell
Dim y1Row, y1Col ' series Y start cell
Dim y2Row, y2Col ' series Y end cell
Dim curChartBy
Dim curSeriesBy
Dim i
Dim iChartCnt
Dim curChartObject As ChartObject
Dim j
Dim cSer

Worksheets(sChartSheet).Activate
Set wsChartSheet = Worksheets(sChartSheet)

x1Row = 2
y1Row = 2
x1Col = 3
y1Col = 4
curChartBy = Cells(2, 1)
curSeriesBy = Cells(2, 2)
For i = 3 To Cells(1, 3).CurrentRegion.Rows.Count + 1
    If Cells(i, 1) <> curChartBy Then
        ' add new chart
        iChartCnt = wsChartSheet.ChartObjects.Count
        Set curChartObject = wsChartSheet.ChartObjects.Add(300, 100 + iChartCnt * 300, 500, 300) ' left, top, width, height
        curChartObject.Chart.ChartType = iChartType
        curChartObject.Chart.HasTitle = True
        curChartObject.Chart.ChartTitle.Characters.Text = curChartBy
        Call Chart_customize(curChartObject, sChartSetting)
        
        ' add series
        x2Row = i - 1
        x2Col = 3
        y2Row = i - 1
        For j = 7 To Cells(i - 1, Cells(1, 3).End(xlToRight).Column()).End(xlToRight).Column
            ' add each series
            y1Col = j
            y2Col = j
            Set cSer = curChartObject.Chart.SeriesCollection.NewSeries
            cSer.XValues = "=" & wsChartSheet.Name & "!" & wsChartSheet.Cells(x1Row, x1Col).Address(ReferenceStyle:=xlR1C1, RowAbsolute:=True, ColumnAbsolute:=True) & ":" & wsChartSheet.Cells(x2Row, x2Col).Address(ReferenceStyle:=xlR1C1, RowAbsolute:=True, ColumnAbsolute:=True)
            cSer.Values = "=" & wsChartSheet.Name & "!" & wsChartSheet.Cells(y1Row, y1Col).Address(ReferenceStyle:=xlR1C1, RowAbsolute:=True, ColumnAbsolute:=True) & ":" & wsChartSheet.Cells(y2Row, y2Col).Address(ReferenceStyle:=xlR1C1, RowAbsolute:=True, ColumnAbsolute:=True)
            If Cells(1, j) <> "" Then
                cSer.Name = Cells(1, j) ' fixLine
                cSer.MarkerStyle = xlMarkerNone
            Else
                cSer.Name = Cells(Cells(i - 1, j).End(xlUp).Row, 2) ' normal Y
                cSer.MarkerStyle = xlMarkerStyleAutomatic
                cSer.MarkerSize = 5
            End If
            cSer.Format.Line.Weight = 1
            
        Next j
        
        ' set new series start location
        x1Row = i
        y1Row = i
        x1Col = 3
        y1Col = 4
        curChartBy = Cells(i, 1)
        curSeriesBy = Cells(i, 2)
    End If
Next i

End Function